apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

apply from: "$project.rootDir/keystore/signing-config.gradle"

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}

def versionMajor = "1.0"

def buildNumber() {
    if (project.hasProperty('buildNumber')) {
        return "." + buildNumber
    } else {
        return ""
    }
}

def buildNumberInt() {
    if (project.hasProperty('buildNumber')) {
        return Integer.parseInt(buildNumber)
    } else {
        return 1
    }
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.valeo.loyalty.android"
        minSdkVersion 21
        //noinspection OldTargetApi
        targetSdkVersion 27
        versionCode buildNumberInt()
        versionName "${versionMajor}${buildNumber()}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled true
            useProguard true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.betaDistributionEmailsFilePath = "$project.rootDir/beta_distribution_emails.txt"
            ext.betaDistributionNotifications = true
        }
    }
    flavorDimensions "server"
    productFlavors {
        staging {
            versionNameSuffix "-staging"
            dimension "server"
            buildConfigField "com.valeo.loyalty.android.app.config.ServerType", "SERVER_TYPE", "com.valeo.loyalty.android.app.config.ServerType.STAGING"
        }
        production {
            versionNameSuffix "-production"
            dimension "server"
            buildConfigField "com.valeo.loyalty.android.app.config.ServerType", "SERVER_TYPE", "com.valeo.loyalty.android.app.config.ServerType.PRODUCTION"
        }
        preproduction {
            versionNameSuffix "-preproduction"
            dimension "server"
            buildConfigField "com.valeo.loyalty.android.app.config.ServerType", "SERVER_TYPE", "com.valeo.loyalty.android.app.config.ServerType.PREPRODUCTION"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.1'

    implementation 'com.jakewharton.timber:timber:4.6.0'
    implementation 'com.jakewharton:butterknife:8.8.1'

    implementation 'net.sourceforge.streamsupport:streamsupport:1.5.3'

    implementation 'com.google.dagger:dagger:2.16'
    implementation 'com.google.dagger:dagger-android-support:2.16'

    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    implementation 'com.google.android.gms:play-services-vision:15.0.2'
    implementation 'com.google.firebase:firebase-core:16.0.0'

    implementation 'com.karumi:dexter:4.2.0'

    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }
    implementation('com.crashlytics.sdk.android:answers:1.4.1@aar') {
        transitive = true
    }
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    annotationProcessor 'com.google.dagger:dagger-compiler:2.16'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.16'

    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    debugImplementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'

    testImplementation 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
